-- Stack machine.

import Array;
import List;
import Fun;
import Collection;
import World;
import State;
import Expr;
import Buffer;

-- Stack code printer. Takes a list of SM instructions, return its
-- string representation.
public fun showSMInsn (i) {
  case i of
    READ         -> sprintf ("READ")
  | WRITE        -> sprintf ("WRITE")
  | BINOP (s)    -> sprintf ("BINOP %s", s)
  | LD    (x)    -> sprintf ("LD %s", x)
  | ST    (x)    -> sprintf ("ST %s", x)
  | CONST (n)    -> sprintf ("CONST %d", n)
  | LABEL (s)    -> sprintf ("LABEL %s", s)
  | JMP   (l)    -> sprintf ("JMP %s", l)
  | CJMP  (c, l) -> sprintf ("CJMP %s, %s", c, l)
  esac
}

public fun showSM (prg) {
  map (fun (i) {showSMInsn (i) ++ "\n"}, prg).stringcat
}

-- Evaluation environment: keeps a mapping between labels and (sub)programs
fun initEvalEnv (insns) {
  var map =
    fix (fun (rec) {
          fun ([m, insns]) {
            case insns of
              {}               -> m
            | LABEL (lab) : tl -> rec ([addMap (m, lab, insns), tl])
            | _ : tl           -> rec ([m, tl])
            esac
          }
        }) $ [emptyMap (compare), insns];
  
  [fun (l) {
     case findMap (map, l) of Some (insns) -> insns esac
  }]
}

-- Accessor function
fun fromLabel (env, lab) {
  env [0] (lab)
}

-- Stack machine interpreter. Takes an environment, an SM-configuration and a program,
-- returns a final configuration
fun eval (env, c@[stack, sigma, world], insns) {
  case insns of
      {} -> c
    | JMP (l) : new_insts -> eval(env, c, fromLabel(env, l))
    | CJMP (x, l) : new_insts -> 
      case stack of
          0 : tail -> 
            case x of 
              Z -> eval(env, c, fromLabel(env, l))
            | NZ-> eval(env, c, new_insts)
            esac
        | _ : tail ->
            case x of 
              NZ -> eval(env, c, fromLabel(env, l))
            | Z-> eval(env, c, new_insts)
            esac  
      esac 
    | com : new_insts -> eval(env,
      case com of 
          BINOP (op) -> 
          case stack of y : (x : tail) -> [evalBinop(op, x, y) : tail, sigma, world] esac
        | CONST (n) -> [n : stack, sigma, world]
        | READ -> case readWorld(world) of [num, new_world] -> [num : stack, sigma, new_world] esac
        | WRITE -> case stack of x : tail -> [tail, sigma, writeWorld(x, world)] esac
        | LD (x) -> [sigma(x): stack, sigma, world]
        | ST (x) -> case stack of y : tail -> [tail, sigma <- [x, y], world] esac
        | LABEL (l) -> c
      esac
    , new_insts)
  esac
}

-- Runs a stack machine for a given input and a given program, returns an output
public fun evalSM (input, insns) {
  eval (initEvalEnv (insns), [{}, emptyState, createWorld (input)], insns)[2].getOutput
}

-- Compilation environment: generates labels
fun makeCompEnv (n) {
  [fun () {
     [sprintf ("L%d", n), makeCompEnv (n+1)]
  }]
}

fun initCompEnv () {
  makeCompEnv (0)
}

-- Accessor function: generates one label
fun genLabel (env) {
  env [0] ()
}

-- Utility function: generates n labels
fun genLabels (env, n) {
  fun inner (env, n) {
    if n == 0
    then {env}
    else case env.genLabel of
           [lab, env] -> lab : inner (env, n-1)
         esac
    fi
  }

  listArray (inner (env, n))
}

-- Compiles an expression into a stack machine code.
-- Takes an expression, returns a list of stack machine instructions
fun compileExpr (expr) {
  case expr of
    Var (x) -> singletonBuffer(LD (x))
  | Const (n) -> singletonBuffer(CONST (n))
  | Binop (op, expr1, expr2) -> compileExpr(expr1) <+> compileExpr(expr2) <+> singletonBuffer(BINOP (op))
  esac
}

-- Compiles a statement into a stack machine code.
-- Takes a statement, returns a list of stack machine
-- instructions.
public fun compileSM (stmt) {
  -- Generates a buffer with an optional label
  --    lab     --- the name of the label
  --    labUsed --- a flag if the lebel is needed
  fun label (lab, labUsed) {
    if labUsed
    then singletonBuffer (LABEL (lab))
    else emptyBuffer ()
    fi
  }

  -- Generates SM code for a given statement
  --    lab  --- a label th go to after the statement is executed
  --    env  --- compilation environment
  --    stmt --- a statement to generate SM code for
  -- Returns a triple:
  --    boolean flag indicating if "lab" was used
  --    updated environment
  --    code buffer  
  fun compile (lab, env, stmt) {
    case stmt of
      Skip           -> [false, env, emptyBuffer ()]
    | Read  (x)      -> [false, env, singletonBuffer (READ) <+ ST (x)]
    | Write (e)      -> [false, env, compileExpr (e) <+ WRITE]
    | Assn  (x, e)   -> [false, env, compileExpr (e) <+ ST (x)]
    | Seq   (s1, s2) ->
        case env.genLabel of
          [s2Lab, env] ->
             case compile (s2Lab, env, s1) of
               [s2LabUsed, env, s1Code] ->
                  case compile (lab, env, s2) of
                    [labUsed, env, s2Code] -> [labUsed, env, s1Code <+> label (s2Lab, s2LabUsed) <+> s2Code]
                  esac
             esac
        esac
    | If (expr, s1, s2) ->
      case env.genLabel of
        [midLab, env] ->
          case compile (lab, env, s1) of
            [_, env, s1Code] ->
              case compile (lab, env, s2) of
                [_, env, s2Code] -> 
                  [true, env, compileExpr(expr) <+ CJMP (Z, midLab) <+> s1Code <+ JMP (lab) <+ LABEL (midLab) <+> s2Code]
              esac
          esac 
      esac
    | While (expr, stmt) -> 
      case env.genLabel of [ls, env] -> case env.genLabel of [le, env] ->
        case compile (le, env, stmt) of
          [_, env, stmtCode] ->
            [false, env, singletonBuffer(JMP (le)) <+ LABEL (ls) <+> stmtCode <+ LABEL (le) <+> compileExpr(expr) <+ CJMP (NZ, ls)]
        esac
      esac esac
    | DoWhile (stmt, expr) ->
      case env.genLabel of [ls, env] -> case env.genLabel of [le, env] ->
        case compile (le, env, stmt) of
          [_, env, stmtCode] ->
            [false, env, singletonBuffer(LABEL (ls)) <+> stmtCode <+ LABEL (le) <+> compileExpr(expr) <+ CJMP (NZ, ls)]
        esac
      esac esac
    esac
  }  

  case initCompEnv ().genLabel of
    [endLab, env] ->
       case compile (endLab, env, stmt) of
         [endLabUsed, _, code] -> getBuffer $  code <+> label (endLab, endLabUsed)
       esac
  esac
}
